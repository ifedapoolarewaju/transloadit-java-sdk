plugins {
    id "com.jfrog.bintray" version "1.7"
}

apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group 'com.transloadit.sdk'

def config = new ConfigSlurper().parse(new File("${projectDir}/src/main/resources/version.properties").toURI().toURL())
version = config.versionNumber
description = "A Java Integration of the Transloadit's(https://transloadit.com) file uploading and encoding service."

allprojects {
    repositories {
        mavenCentral()
        jcenter()
    }
}


dependencies {
    implementation 'org.jetbrains:annotations:13.0'
    implementation 'io.tus.java.client:tus-java-client:0.4.1'
    implementation 'joda-time:joda-time:2.9.7'
    implementation 'com.squareup.okhttp3:okhttp:3.6.0'
    // implementation 'org.json:json:20160212'
    implementation 'org.json:json:20210307'
    implementation 'commons-codec:commons-codec:1.9'
    implementation 'io.socket:socket.io-client:1.0.0'
    testImplementation 'junit:junit:4.12'
    testImplementation  'org.mock-server:mockserver-netty:3.9.17'
    testImplementation  'org.mockito:mockito-core:2.+'

}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

def pomConfig = {
    name 'transloadit'
    url 'https://github.com/transloadit/java-sdk'
    licenses {
        license {
            name 'The MIT License (MIT)'
            url 'http://opensource.org/licenses/MIT'
        }
    }

    developers {
        developer {
            id 'ifedapoolarewaju'
            name 'Ifedapo Olarewaju'
            email 'ifedapoolarewaju@gmail.com'
        }
    }

    scm {
        url 'https://github.com/transloadit/java-sdk'
        connection 'https://github.com/transloadit/java-sdk'
        developerConnection 'https://github.com/transloadit/java-sdk'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId project.getGroup()
            artifactId 'transloadit'
            version project.getVersion()
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('description', project.getDescription())
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    dryRun = true
    pkg {
        repo = 'maven'
        name = 'transloadit'
        userOrg = 'transloadit'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/transloadit/java-sdk.git'
        websiteUrl = 'https://github.com/transloadit/java-sdk'
        issueTrackerUrl = 'https://github.com/transloadit/java-sdk/issues'
        publicDownloadNumbers = true
        version {
            name = project.getVersion()
            desc = project.getDescription()
            vcsTag = project.getVersion()
        }
    }
}